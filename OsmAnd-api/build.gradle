apply plugin: 'com.android.library'
apply plugin: 'ivy-publish'

android {
    compileSdk 34
    buildToolsVersion = "34.0.0"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 2
        versionName "2.0"
        namespace = "net.osmand.aidlapi"
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            aidl.srcDirs = ["src"]
            java.srcDirs = ["src"]
        }
    }
    buildFeatures {
        aidl true
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant('debug') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

version = System.getenv("OSMAND_BINARIES_IVY_REVISION") ?: "master-snapshot"
afterEvaluate {
    publishing {
        repositories {
            ivy {
                url = System.getenv("OSMAND_BINARIES_IVY_ROOT") ?: "./"
            }
        }

        publications {
             release(IvyPublication) {
                // Applies the component for the release build variant.
                from components.release
                organisation = 'net.osmand'
                module = 'android-aidl-lib'
             }
             debug(IvyPublication) {
                // Applies the component for the release build variant.
                from components.debug
                organisation = 'net.osmand'
                module = 'android-aidl-lib'
             }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.annotation:annotation:1.6.0'
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
