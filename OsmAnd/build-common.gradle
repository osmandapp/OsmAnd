apply from: rootProject.file("versions.gradle")

tasks.register('printc') {
	configurations.each { if (it.isCanBeResolved()) println it.name }
}

android {
    compileSdk osmand_compileSdk
    buildToolsVersion = osmand_buildToolsVersion
    // compileNdkVersion "android-ndk-r17b"
    namespace = "net.osmand.plus"
    defaultConfig {
        targetSdkVersion osmand_targetSdk
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

	lintOptions {
		abortOnError false
	}

	// related to kuromoji
	//packagingOptions {
	//	exclude '/META-INF/CONTRIBUTORS.md'
	//	exclude '/META-INF/LICENSE.md'
	//	exclude '/META-INF/NOTICE.md'
	//}

	// This is from OsmAndCore_android.aar - for some reason it's not inherited
	aaptOptions {
		// Don't compress any embedded resources
		noCompress "qz"
		cruncherEnabled = false
		// Flag notifies aapt to keep the attribute IDs around
		// additionalParameters "--no-version-vectors"
	}

	testOptions {
		animationsDisabled = true
	}

	sourceSets {
		main {
			manifest.srcFile "AndroidManifest.xml"
			jni.srcDirs = []
			jniLibs.srcDirs = ["libs"]
			aidl.srcDirs = ["src"]
			java.srcDirs = ["src"]
			resources.srcDirs = ["src"]
			res.srcDirs = ["res"]
			assets.srcDirs = ["assets"]
		}
		androidTest {
			java.srcDirs = ["test/java"]
			res.srcDirs = ["test/res"]
			assets.srcDirs = ["test/assets"]
		}
		legacy {
			jniLibs.srcDirs = ["libc++"]
		}
	}

	flavorDimensions "coreversion", "abi"
	productFlavors {
		// ABI
		armv7 {
			dimension "abi"
			ndk {
				abiFilter 'armeabi-v7a'
			}
		}
		arm64 {
			dimension "abi"
			ndk {
				abiFilter 'arm64-v8a'
			}
		}
		x86 {
			dimension "abi"
			ndk {
				abiFilters 'x86', 'x86_64'
			}
		}
		armonly {
			dimension "abi"
			ndk {
				//noinspection ChromeOsAbiSupport
				abiFilters 'arm64-v8a', 'armeabi-v7a'
			}
		}
		fat {
			dimension "abi"
			ndk {
				abiFilters 'arm64-v8a', 'x86', 'x86_64', 'armeabi-v7a'
			}
		}

		// CoreVersion
		// Build that doesn't include 3D OpenGL
		legacy {
			dimension "coreversion"
		}
	}

	buildTypes {
		debug {
			debuggable true
		}
		release {
			debuggable false
		}
	}
}

clean.doFirst {
	delete fileTree('assets') {
		include '*.obf'
		include 'fonts/**'
		include 'voice/**'
	}
}

tasks.register('validateTranslations') {
	println "Validating translations"
	def taskStartTime = System.currentTimeMillis()

	def pattern = /%(\d+\$)?[sSdD]/
	def files = ["strings.xml", "phrases.xml"]
	def defaultPlaceholders = [:]
	def parser = new XmlParser()

	def normalizePlaceholders = { placeholders -> placeholders.collect { it.toLowerCase() }.toSet() }

	files.each { fileName ->
		def file = new File(file("res/values"), fileName)
		if (file.exists()) {
			parser.parse(file).string.each { node ->
				def placeholders = (node.text() ?: "").findAll(pattern)
				defaultPlaceholders[node.@name] = normalizePlaceholders(placeholders)
			}
		} else {
			println "Skipping missing default file: ${file.path}"
		}
	}

	def issues = []
	def directories = file("res").listFiles().findAll {
		it.isDirectory() && it.name.startsWith("values-")
	}

	directories.each { dir ->
		files.each { fileName ->
			def file = new File(dir, fileName)
			if (file.exists()) {
				def placeholders = [:]
				parser.parse(file).string.each { node ->
					def translationPlaceholders = (node.text() ?: "").findAll(pattern)
					placeholders[node.@name] = normalizePlaceholders(translationPlaceholders)
				}

				defaultPlaceholders.each { key, defaultValues ->
					if (placeholders.containsKey(key)) {
						def translationValues = placeholders[key]
						if (defaultValues != translationValues) {
							issues << "Mismatch in ${file.path}: '${key}' -> Default: ${defaultValues}, Translation: ${translationValues}"
						}
					}
				}
			}
		}
	}

	if (!issues.isEmpty()) {
		issues.each { println "ERROR: $it" }
		throw new GradleException("Translation validation failed with ${issues.size()} issue(s).")
	}
	def validationEndTime = System.currentTimeMillis()
	println "Translations validated in ${(validationEndTime - taskStartTime)} ms"
}

tasks.register('downloadWorldMiniBasemap', Download) {
	src 'https://builder.osmand.net/basemap/World_basemap_mini_2.obf'
	dest 'assets/World_basemap_mini.obf'
	onlyIfModified true
}

tasks.register('downloadWorldMiniBasemapAnt') {
	doLast {
		ant.get(src: 'https://builder.osmand.net/basemap/World_basemap_mini_2.obf', dest: 'assets/World_basemap_mini.obf', skipexisting: 'true')
	}
}

tasks.register('collectVoiceAssets', Sync) {
	from "../../resources/voice"
	into "assets/voice"
	include "**/*.js"
}

tasks.register('collectFonts', Copy) {
	from "../../resources/rendering_styles/fonts"
//	from "../../resources/rendering_styles/fonts/OpenSans"
	into "assets/fonts"
	include "*.ttf"
}

tasks.register('copyBundledAssets', Copy) {
	from("../../resources/bundled_assets.json") {
		include "bundled_assets.json"
	}
	into "assets"
}

tasks.register('copyProjDb', Copy) {
	from("../../resources/proj") {
		include "proj.db"
	}
	into "assets"
}

tasks.register('copyColorPalettes', Copy) {
	from "../../resources/color-palette"
	into "assets/color-palette"
	include "*.*"
}

tasks.register('copyPoiCategories', Copy) {
	from("../../resources/poi") {
		include "poi_categories.json"
	}
	into "assets"
}

tasks.register('copyRouteActivities', Copy) {
	from("../../resources/poi") {
		include "activities.json"
	}
	into "assets"
}

tasks.register('copyMapShaderIcons', Sync) {
	// from "../../resources/rendering_styles/style-icons/map-shaders-png"
	// into "res/"
	from "../../resources/rendering_styles/style-icons/map-shaders-vector"
	into "res/drawable"
	include "**/*.png", "**/*.xml"
	preserve {
		include '**/*'
		exclude "**/h_*"
	}
}

tasks.register('copyMapPOIIcons', Sync) {
	from "../../resources/rendering_styles/style-icons/map-icons-vector"
	into "res/drawable/"
	// from "../../resources/rendering_styles/style-icons/map-icons-png"
	// into "res/"

	include "**/*.png", "**/*.xml"
	preserve {
		include '**/*'
		exclude "**/mm_*"
	}
}

tasks.register('copyLargePOIIcons', Sync) {
	from "../../resources/rendering_styles/style-icons/poi-icons-vector"
	into "res/drawable/"
	include "**/*.png", "**/*.xml"
	preserve {
		include '**/*'
		exclude "**/mx_*"
	}
}

tasks.register('collectTestResources', Copy) {
	into "test/assets/"
	from("../../resources/test-resources/ui/") {
		include "*"
		include "/search/*"
		include "/routing/*"
	}
}

tasks.register('copy3DModels', Copy) {
	from "../../resources/models"
	into "assets/models"
	include "**/*.*"
}

tasks.register('collectExternalResources') {
	dependsOn collectVoiceAssets,
			collectFonts,
			copyMapShaderIcons,
			copyMapPOIIcons,
			copyLargePOIIcons,
			validateTranslations,
			copyBundledAssets,
			copyProjDb,
			copyColorPalettes,
			copyPoiCategories,
			copyRouteActivities,
			downloadWorldMiniBasemap,
			collectTestResources,
			copy3DModels
}

// Legacy core build
import org.apache.tools.ant.taskdefs.condition.Os

tasks.register('buildOsmAndCore', Exec) {
	Gradle gradle = getGradle()
	String tskReqStr = gradle.getStartParameter().getTaskRequests().toString().toLowerCase()
	String flavour = ""
	if (!tskReqStr.contains("fat")) {
		if (tskReqStr.contains("arm64")) {
			flavour = flavour.length() == 0 ? "ARM64_ONLY" : ""
		}
		if (tskReqStr.contains("armv7")) {
			flavour = flavour.length() == 0 ? "ARMV7_ONLY" : ""
		}
		if (tskReqStr.contains("armonly")) {
			flavour = flavour.length() == 0 ? "ARM_ONLY" : ""
		}
		if (tskReqStr.contains("x86")) {
			flavour = flavour.length() == 0 ? "X86_ONLY" : ""
		}
	}

	description "Build Legacy OsmAndCore"

	if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
		if (flavour.length() > 0) {
			environment "$flavour", "1"
		}
		commandLine "bash", file("./old-ndk-build.sh").getAbsolutePath()
	} else {
		commandLine "cmd", "/c", "echo", "Not supported"
	}
}

tasks.register('cleanupDuplicatesInCore') {
	dependsOn(buildOsmAndCore)

	// Capture paths at configuration time using Gradle's layout API
	def targetDirs = [
			'armeabi-v7a': [source: layout.projectDirectory.file("libs/armeabi-v7a/libc++_shared.so"), dest: layout.projectDirectory.file("libc++/armeabi-v7a/libc++_shared.so")],
			'arm64-v8a': [source: layout.projectDirectory.file("libs/arm64-v8a/libc++_shared.so"), dest: layout.projectDirectory.file("libc++/arm64-v8a/libc++_shared.so")],
			'x86': [source: layout.projectDirectory.file("libs/x86/libc++_shared.so"), dest: layout.projectDirectory.file("libc++/x86/libc++_shared.so")],
			'x86_64': [source: layout.projectDirectory.file("libs/x86_64/libc++_shared.so"), dest: layout.projectDirectory.file("libc++/x86_64/libc++_shared.so")]
	]

	doLast {
		targetDirs.each { arch, paths ->
			def destFile = paths.dest.asFile
			def destDir = destFile.getParentFile()

			destDir.mkdirs()
			if (paths.source.asFile.exists()) {
				if (!paths.source.asFile.renameTo(destFile)) {
					logger.error("Failed to move file from ${paths.source.asFile} to ${destFile}.")
				}
			}
		}
	}
}

tasks.register('appStart', Exec) {
	// linux
	commandLine 'adb', 'shell', 'am', 'start', '-n', 'net.osmand.plus/net.osmand.plus.activities.MapActivity'
	// windows
	// commandLine 'cmd', '/c', 'adb', 'shell', 'am', 'start', '-n', 'net.osmand.plus/net.osmand.plus.activities.MapActivity'
}

dependencies {
	implementation project(path: ':OsmAnd-java', configuration: 'android')
	implementation project(':OsmAnd-api')

	implementation project(':OsmAnd-shared')
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	implementation("androidx.sqlite:sqlite-framework:2.3.1")

	implementation 'androidx.multidex:multidex:2.0.1'
	implementation 'androidx.gridlayout:gridlayout:1.0.0'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'com.google.android.material:material:1.9.0'
	implementation 'androidx.browser:browser:1.0.0'
	implementation 'androidx.preference:preference:1.1.0'
	implementation 'androidx.lifecycle:lifecycle-process:2.6.0'
	implementation "androidx.activity:activity:1.10.1"
//	implementation "androidx.core:core:1.16.0"
	implementation fileTree(include: ['gnu-trove-osmand.jar', 'icu4j-49_1_patched.jar'], dir: 'libs')

	implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
	implementation 'commons-codec:commons-codec:1.11'
	implementation 'org.apache.commons:commons-compress:1.17'
	implementation 'com.moparisthebest:junidecode:0.1.1'
	implementation 'org.immutables:gson:2.5.0'
	implementation 'com.vividsolutions:jts-core:1.14.0'
	implementation 'com.google.openlocationcode:openlocationcode:1.0.4'
	implementation 'com.android.billingclient:billing:8.0.0'
	// turn off for now
	//implementation 'com.atilika.kuromoji:kuromoji-ipadic:0.9.0'
	implementation 'com.squareup.picasso:picasso:2.71828'
	//implementation("io.coil-kt.coil3:coil-core:3.1.0")
	//implementation("io.coil-kt.coil3:coil-network-okhttp:3.1.0")

	implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
	implementation "net.osmand:antpluginlib:3.8.0@aar"
	// JS core
	implementation group: 'org.mozilla', name: 'rhino', version: '1.7.9'
// size restrictions
// implementation 'com.ibm.icu:icu4j:50.1'
// implementation 'net.sf.trove4j:trove4j:3.0.3'

	legacyImplementation "net.osmand:OsmAndCore_android:master-snapshot@jar"

	implementation("com.getkeepsafe.taptargetview:taptargetview:1.13.2") {
		exclude group: 'com.android.support'
	}

	debugImplementation "net.osmand:MPAndroidChart:custom-snapshot-debug@aar"
	releaseImplementation "net.osmand:MPAndroidChart:custom-snapshot-release@aar"

	implementation("com.github.HITGIF:TextFieldBoxes:1.4.5") {
		exclude group: 'com.android.support'
	}
	implementation('com.github.scribejava:scribejava-apis:7.1.1')
	implementation 'com.jaredrummler:colorpicker:1.1.0'
	implementation 'org.bouncycastle:bcpkix-jdk15on:1.56'
	implementation 'com.google.android.play:review:2.0.1'

	implementation "androidx.car.app:app:1.4.0"
	implementation "androidx.car.app:app-projected:1.4.0"

	implementation 'com.google.android.gms:play-services-location:21.3.0'

	//implementation "androidx.tracing:tracing:1.1.0"
	//debugImplementation 'androidx.test:monitor:1.6.1'
	androidTestImplementation "androidx.test:runner:1.5.2"
	androidTestImplementation "androidx.test:rules:1.5.0"
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
	//debugImplementation("androidx.test:core:1.5.0")
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation ('androidx.test.espresso:espresso-contrib:3.5.1') {
		exclude module: "protobuf-lite"
	}
	implementation 'net.sf.marineapi:marineapi:0.12.0'
	implementation 'com.facebook.shimmer:shimmer:0.5.0@aar'
}
