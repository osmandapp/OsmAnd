plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '2.0.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.0'
    id 'ivy-publish'    
}

kotlin {
    jvm {
        // Experimental Kotlin Gradle Plugin API opt-in
        compilerOptions {
            jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
            freeCompilerArgs.add("-Xjvm-default=all")
        }
    }

    def sqliteVersion = "2.3.1"
    def serializationVersion = "1.6.3"
    def coroutinesCoreVersion = "1.8.1"
    def datetimeVersion = "0.6.0"
    def okioVersion = "3.9.0"
    def kxml2Version = "2.3.0"
    def sqliterVersion = "1.3.1"
    def sqliteJDBCVersion = "3.34.0"
    def commonLoggingVersion = "1.2"
    def statelyVersion = "2.1.0"

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serializationVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesCoreVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-datetime:$datetimeVersion"
                implementation "com.squareup.okio:okio:$okioVersion"
                implementation("co.touchlab:stately-concurrent-collections:$statelyVersion")
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
                implementation "net.sf.kxml:kxml2:$kxml2Version"
                implementation "org.xerial:sqlite-jdbc:$sqliteJDBCVersion"
                implementation "commons-logging:commons-logging:$commonLoggingVersion"
            }
        }
    }
}


configurations {
    jvmPublicConfig  { 
        extendsFrom(jvmRuntimeClasspath)
    }
}

artifacts {
    jvmPublicConfig jvmJar
}


version = System.getenv("OSMAND_BINARIES_IVY_REVISION") ?: "master-snapshot"
publishing {
    publications {
        ivyJava(IvyPublication) {
            from components.java

            // Define the artifact with a specific name and classifier
            artifact("build/libs/OsmAnd-shared-jvm-master-snapshot.jar") {
                name = 'OsmAnd-shared-jvm'
                type = 'jar'
                extension = 'jar'
                classifier = version
            }

            // Configure Ivy coordinates
            organisation 'net.osmand'
            module 'OsmAnd-shared'
            revision version

            // Optionally define Ivy configurations
            configurations {
                jvmRuntime
            }
        }
    }

    repositories {
		ivy {
			url = System.getenv("OSMAND_BINARIES_IVY_ROOT") ?: "./"
		}
	}
}
