apply plugin: 'com.android.application'

android {
	compileSdkVersion 28
	buildToolsVersion "28.0.3"

	signingConfigs {
		development {
			storeFile file("../../keystores/debug.keystore")
			storePassword "android"
			keyAlias "androiddebugkey"
			keyPassword "android"
		}
		release {
			storeFile file("/var/lib/jenkins/osmand_key")
			storePassword System.getenv("OSMAND_APK_PASSWORD")
			keyAlias "osmand"
			keyPassword System.getenv("OSMAND_APK_PASSWORD")
		}

	}

	defaultConfig {
		applicationId "net.osmand.parkingPlugin"
		minSdkVersion 15
		targetSdkVersion 28
		versionCode 9
		versionName "1.0"
	}

	lintOptions {
		abortOnError false
	}

	sourceSets {
		main {
			manifest.srcFile "AndroidManifest.xml"
			jni.srcDirs = []
			jniLibs.srcDirs = []
			aidl.srcDirs = ["src"]
			java.srcDirs = ["src"]
			renderscript.srcDirs = ["src"]
			res.srcDirs = ["res"]
			assets.srcDirs = ["assets"]
		}
	}

	buildTypes {
		debug {
			signingConfig signingConfigs.development
		}
		release {
			signingConfig signingConfigs.release
		}
	}
}

repositories {
	ivy {
		name = "OsmAndBinariesIvy"
		url = "http://builder.osmand.net"
		layout "pattern", {
			artifact "ivy/[organisation]/[module]/[revision]/[artifact]-[revision].[ext]"
		}
	}
}

def analytics = System.getenv("USE_FIREBASE_FOR_PLUGINS")

dependencies {
	if (analytics) {
		implementation 'com.google.firebase:firebase-core:12.0.1'
		implementation 'com.google.firebase:firebase-config:12.0.1'
	}
	implementation 'com.android.support:appcompat-v7:28.0.0'
	implementation 'com.android.support:design:28.0.0'
}

if (analytics) {
	println "Apply GMS plugin"
	apply plugin: 'com.google.gms.google-services'
}
